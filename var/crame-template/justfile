set dotenv-load

exeName := `default=$(basename $PWD); echo ${EXE_NAME:-$default}`
srcFiles := `fd -e c -p 'src|lib' --exec-batch echo`
testFiles := `fd -e c --exclude 'src/main.c' -p 'src|lib|tests' --exec-batch echo`

run: build
    @echo "$(tput setaf 2)Running $(tput setaf 7){{exeName}}$(tput sgr0):\n"
    @./target/{{exeName}}

test: _build-test
    @echo "$(tput setaf 2)Testing$(tput sgr0):"
    @-./target/test 

build:
    @echo "$(tput setaf 2)Building $(tput setaf 7){{exeName}}$(tput sgr0):\n"
    @mkdir -p ./target
    cc {{srcFiles}} -o ./target/{{exeName}}
    @echo

_build-test:
    @mkdir -p ./target
    @cc {{testFiles}} -o ./target/test

clean:
    @echo "Cleaning target/"
    @rm -f target/*